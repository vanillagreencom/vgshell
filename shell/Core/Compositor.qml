pragma Singleton
import QtQuick
import Quickshell
import Quickshell.Io
import Quickshell.Hyprland
import "Dispatch.js" as Dispatch

// The one place the shell dispatches to Hyprland. Dispatch.js builds every
// request in the session's syntax and refuses an argument that could break
// out of it; the request runs through hyprctl and its reply is judged by
// text: Hyprland answers a refused dispatcher with exit 0 and an error
// sentence, so exit status alone says nothing. One process drains a bounded
// queue of hyprctl argument lists, preserving each request until all of its
// completion signals land: a dispatch is `hyprctl dispatch <request>`, and
// a keyboard layout switch, which is no dispatcher, its own argv from
// Dispatch.switchLayoutRequest.
// `reveal` brings an application's window into view; its decisions are
// Dispatch.js's and the Hyprland facts it rests on are runtime-hyprland.md's.
Singleton {
    id: root

    // The argv running, or null, and the callback that takes its answer;
    // each waiting request as { argv, done }, in order.
    property var pending: null
    property var pendingDone: null
    // Whether the running request's caller judges a reply other than `ok`
    // itself, so it is not logged here.
    property bool pendingJudged: false
    property string reply: ""
    property var queue: []
    property var completion: null
    property var inputSurfaces: []
    property var inputObservation: null
    // Quickshell 0.3.1 builds its desktop entry index on the first read of
    // DesktopEntries and hands the scan over by a queued call, so that read
    // returns an empty list (src/core/desktopentry.cpp). Read when the
    // singleton is built, the index is filled before an observation's reply.
    readonly property var desktopApplications: DesktopEntries.applications

    // Host-owned raw QQuickWindows supply activation, including layer focus
    // absent from Hyprland's activewindow reply. Each host releases its record.
    function inputSurface(window) {
        if (window === null) return () => {};
        const record = { window: window };
        inputSurfaces = inputSurfaces.concat([record]);
        return () => { inputSurfaces = inputSurfaces.filter(r => r !== record); };
    }

    function observeInput(point, done) {
        if (typeof done !== "function") throw new Error("refused: input=callback");
        if (inputObservation !== null) { done({ ok: false, error: "refused: input=busy" }); return; }
        inputObservation = { point: point, done: done };
        inputReader.read(["hyprctl", "--batch", Dispatch.INPUT_STATE_REQUEST], null);
        inputDeadline.start();
    }

    function finishInput(value) {
        const pending = inputObservation;
        inputObservation = null;
        inputDeadline.stop();
        if (pending !== null) pending.done(value);
    }

    HyprctlReader {
        id: inputReader
        label: "input"
        onReadDone: (request, text, failure) => {
            if (root.inputObservation === null) return;
            const protectedKeyboard = root.inputSurfaces.some(record => record.window !== null && record.window.visible && record.window.active);
            const entries = root.desktopApplications.values.map(entry => ({ id: entry.id.replace(/\.desktop$/, "").toLowerCase(),
                startupClass: entry.startupClass.toLowerCase(), terminal: entry.categories.indexOf("TerminalEmulator") !== -1 }));
            root.finishInput(failure === "" ? Dispatch.inputTarget(text, root.inputObservation.point, protectedKeyboard, entries)
                : { ok: false, error: "refused: input=read-failed " + failure });
        }
    }
    Timer { id: inputDeadline; interval: 2000; onTriggered: root.finishInput({ ok: false, error: "refused: input=timeout" }) }

    // The windows and the monitors Hyprland has now: DONE takes
    // Dispatch.windowState's answer, or { ok: false, error } when the read
    // fails. Every caller waiting when a read ends takes that read, which
    // started after each of them asked: HyprctlReader drops a running reply
    // when a newer request comes.
    property var windowsWaiting: []

    function readWindows(done) {
        if (typeof done !== "function") throw new Error("refused: windows=callback");
        windowsWaiting = windowsWaiting.concat([done]);
        windowsReader.read(["hyprctl", "--batch", Dispatch.WINDOW_STATE_REQUEST], null);
    }

    HyprctlReader {
        id: windowsReader
        label: "windows"
        onReadDone: (request, text, failure) => {
            const waiting = root.windowsWaiting;
            root.windowsWaiting = [];
            const state = failure === "" ? Dispatch.windowState(text) : { ok: false, error: "refused: windows=read-failed " + failure };
            for (const done of waiting) done(state);
        }
    }

    // The screen a surface lands on when nothing chose one: the focused
    // monitor, or the first screen when Hyprland names none Quickshell
    // knows, or null with no screen at all.
    function focusedScreen() {
        const monitor = Hyprland.focusedMonitor;
        const screens = Quickshell.screens;
        for (let i = 0; i < screens.length; i++)
            if (monitor !== null && screens[i].name === monitor.name) return screens[i];
        return screens.length > 0 ? screens[0] : null;
    }

    // Send one dispatcher Dispatch.js knows. Returns `ok` once the request
    // is accepted, or the keyed refusal; the reply is judged when it lands.
    function send(name, args) {
        return enqueueRequest(Dispatch.request(name, args, Hyprland.usingLua), null);
    }

    // Enter or leave the key capture pass-through submap, `enter`,
    // `enterAnyWindow` or `leave` (Dispatch.passthroughRequest);
    // KeyCapture.qml is the one caller. Answers as `send` does, and an
    // accepted request hands DONE its answer once it ran: `ok`, Hyprland's
    // reply text otherwise, or a
    // keyed `dispatch-start=failed` or `exit=<code>` line, with
    // ` reply=<JSON text>` after it when hyprctl, which exits non-zero on a
    // refused request, printed the refusal.
    function passthrough(verb, done) {
        return enqueueRequest(Dispatch.passthroughRequest(verb, Hyprland.usingLua), done);
    }

    // Show or hide a pad of the Hyprland layer (Dispatch.padRequest);
    // answers as `passthrough` does, and DONE takes the answer once the
    // request ran. A DONE judges the answer, as the layer's `vgs-pad=<key>`
    // refusals are answers a plugin acts on, so only a toggle without one
    // logs a refusal.
    function togglePad(name, screen, done) {
        return enqueueRequest(Dispatch.padRequest(name, screen, Hyprland.usingLua), done, typeof done === "function");
    }

    function enqueueRequest(r, done, judged) {
        if (!r.ok) {
            console.error("compositor: " + r.error);
            return r.error;
        }
        return enqueue(["hyprctl", "dispatch", r.request], done, judged === true);
    }

    // Switch every keyboard's layout: `next`, `prev` or an index. Returns
    // `ok` once the request is accepted, or the keyed refusal; the reply is
    // judged when it lands, as a dispatch's is.
    function switchLayout(target) {
        const r = Dispatch.switchLayoutRequest(target);
        if (!r.ok) {
            console.error("compositor: " + r.error);
            return r.error;
        }
        return enqueue(r.argv, null);
    }

    function enqueue(argv, done, judged) {
        if (queue.length >= Dispatch.QUEUE_LIMIT) {
            const error = "refused: dispatch-queue=full limit=" + Dispatch.QUEUE_LIMIT + " request=" + JSON.stringify(argv);
            console.error("compositor: " + error);
            return error;
        }
        queue = queue.concat([{ argv: argv, done: typeof done === "function" ? done : null, judged: judged === true }]);
        drain();
        return "ok";
    }

    function drain() {
        if (pending !== null || queue.length === 0) return;
        pending = queue[0].argv;
        pendingDone = queue[0].done;
        pendingJudged = queue[0].judged;
        queue = queue.slice(1);
        completion = null;
        reply = "";
        proc.command = pending;
        proc.running = true;
    }

    // ------------------------------------------------------------ reveal

    // The reveal in progress: { addresses, named, reading, layerWait }, or
    // null; `reading` once it waits for the state read, `layerWait` while
    // it waits for the shell's keyboard layers to go. A newer reveal replaces
    // it, since the user asked for the newer place.
    property var revealing: null
    // A state read was asked for while one ran, so the answer follows
    // the state after the newer request.
    property bool rereadState: false
    // The shell's layer surfaces that hold the keyboard exclusively, each
    // a record its host releases when the surface goes. Hyprland refuses a
    // window the keyboard while such a layer holds it, and gives the
    // keyboard back to the window focused before once the layer goes, so a
    // reveal reads the state and focuses only once none is left.
    property var keyboardLayers: []

    // Bring one of `addresses`, one application's windows, into view: the
    // workspace, a hidden special workspace, a background group tab or
    // another monitor, as Hyprland's focus dispatcher does for the window
    // it focuses. The window is the one the application asked for, else
    // the one the user focused last, and nothing moves when it is already
    // the active window on the screen (Dispatch.revealTarget, from one
    // batched read of the clients, the active window and the monitors).
    // With `awaitSender`, after the caller delivered an action to the
    // application, the shell first waits up to Dispatch.SENDER_WAIT_MS for
    // Hyprland to report that the application focused one of those windows
    // itself, and then moves nothing, so the view never switches twice.
    // While a shell layer holds the keyboard exclusively, the read and the
    // focus wait until the last such layer goes, which its host reports;
    // a layer that never goes keeps the reveal waiting until a newer one
    // replaces it. Returns `ok` once accepted, or the keyed refusal; each
    // outcome logs one line, `compositor: reveal=<shell|sender|shown|none>`.
    function reveal(addresses, awaitSender) {
        const judged = Dispatch.revealRequest(addresses);
        if (!judged.ok) {
            console.error("compositor: " + judged.error);
            return judged.error;
        }
        if (revealing !== null) endReveal("superseded", "");
        revealing = { addresses: judged.addresses, named: "", reading: false };
        if (awaitSender === true) revealWait.restart();
        else readState();
        return "ok";
    }

    function endReveal(by, address) {
        revealWait.stop();
        revealing = null;
        console.info("compositor: reveal=" + by + " address=" + (address === "" ? "none" : address));
    }

    Timer {
        id: revealWait
        interval: Dispatch.SENDER_WAIT_MS
        onTriggered: root.readState()
    }

    Connections {
        target: Hyprland
        function onRawEvent(event) {
            if (root.revealing === null || !revealWait.running) return;
            const seen = Dispatch.revealEvent(root.revealing.addresses, event.name, event.data);
            if (seen.by === "sender") {
                root.endReveal("sender", seen.address);
            } else if (seen.by === "named") {
                revealWait.stop();
                root.revealing.named = seen.address;
                root.readState();
            }
        }
    }

    // A layer surface that holds the keyboard exclusively, from its host:
    // returns the release its host calls as the surface goes.
    function keyboardLayer() {
        const record = {};
        keyboardLayers = keyboardLayers.concat([record]);
        return () => {
            keyboardLayers = keyboardLayers.filter(r => r !== record);
            // On the next turn, once the surface's own teardown has run.
            if (keyboardLayers.length === 0) Qt.callLater(root.layersGone);
        };
    }

    function layersGone() {
        if (keyboardLayers.length === 0 && revealing !== null && revealing.layerWait === true) {
            revealing.layerWait = false;
            readState();
        }
    }

    function readState() {
        if (keyboardLayers.length > 0) {
            revealing.layerWait = true;
            return;
        }
        revealing.reading = true;
        if (stateProc.running) {
            rereadState = true;
            return;
        }
        stateProc.completion = null;
        stateProc.running = true;
    }

    function stateRead(completion, text) {
        if (rereadState) {
            rereadState = false;
            if (revealing !== null && revealing.reading) {
                stateProc.completion = null;
                stateProc.running = true;
            }
            return;
        }
        if (revealing === null || !revealing.reading) return;
        if (completion === null || completion.code !== 0) {
            console.error("compositor: reveal state=unread exit=" + (completion === null ? "start-failed" : completion.code));
            endReveal("none", "");
            return;
        }
        const state = Dispatch.revealState(text);
        if (!state.ok) {
            console.error("compositor: " + state.error);
            endReveal("none", "");
            return;
        }
        const target = Dispatch.revealTarget(state, revealing.addresses, revealing.named);
        switch (target.state) {
        case "reveal":
            send("focusWindow", [target.address]);
            endReveal("shell", target.address);
            break;
        case "shown":
            endReveal("shown", target.address);
            break;
        case "none":
            endReveal("none", "");
            break;
        default:
            throw new Error("compositor: reveal state " + JSON.stringify(target.state) + " is not one of reveal, shown, none");
        }
    }

    Process {
        id: stateProc
        property var completion: null
        command: ["hyprctl", "--batch", Dispatch.REVEAL_STATE_REQUEST]
        stdout: StdioCollector { id: stateOut }
        onExited: (code, status) => { completion = { code: code, status: status }; }
        onRunningChanged: {
            if (running) return;
            root.stateRead(completion, stateOut.text);
        }
    }

    Process {
        id: proc
        stdout: StdioCollector {
            onStreamFinished: {
                root.reply = text.trim();
                if (root.reply !== "ok" && !root.pendingJudged)
                    console.error("compositor: request " + JSON.stringify(root.pending) + " answered " + JSON.stringify(root.reply));
            }
        }
        onExited: (code, status) => {
            root.completion = { code: code, status: status };
            if (code !== 0)
                console.error("compositor: hyprctl exited " + code + " for " + JSON.stringify(root.pending));
        }
        onRunningChanged: {
            if (running || root.pending === null) return;
            if (root.completion === null)
                console.error("compositor: dispatch-start=failed request=" + JSON.stringify(root.pending));
            const done = root.pendingDone;
            const answer = root.completion === null ? "dispatch-start=failed" : root.completion.code !== 0 ? "exit=" + root.completion.code + (root.reply === "" ? "" : " reply=" + JSON.stringify(root.reply)) : root.reply;
            root.pending = null;
            root.pendingDone = null;
            root.pendingJudged = false;
            if (done !== null) done(answer);
            root.drain();
        }
    }
}
